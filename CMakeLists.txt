cmake_minimum_required(VERSION 3.16.0)
project(slipperygg)

if (NOT WIN32)
  set(CMAKE_C_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} -fvisibility=hidden -fno-exceptions -Wfatal-errors")
  set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_C_FLAGS_RELEASE} -fno-rtti")
endif()

add_compile_options($<$<AND:$<CONFIG:Release>,$<CXX_COMPILER_ID:GNU>,$<C_COMPILER_ID:GNU>>:-s>)

set(CMAKE_CXX_STANDARD 20)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

if (CMAKE_CXX_COMPILER_ID STREQUAL "GNU")
  set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} --no-gnu-unique")
endif()

file(GLOB_RECURSE SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/*.cpp")

if (NOT WIN32)
  list(REMOVE_ITEM SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/imgui/imgui_impl_dx9.cpp")
  list(REMOVE_ITEM SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/imgui/imgui_impl_win32.cpp")
  list(REMOVE_ITEM SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/Hooks/MinHook.cpp")
else()
  list(REMOVE_ITEM SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/imgui/imgui_impl_opengl3.cpp")
  list(REMOVE_ITEM SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/imgui/imgui_impl_sdl.cpp")
  list(APPEND SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/MinHook/buffer.c")
  list(APPEND SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/MinHook/hde/hde32.c")
  list(APPEND SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/MinHook/hde/hde64.c")
  list(APPEND SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/MinHook/hook.c")
  list(APPEND SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/MinHook/trampoline.c")
endif()

list(REMOVE_ITEM SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/pch.cpp") 
list(REMOVE_ITEM SOURCES "${CMAKE_SOURCE_DIR}/slipperygg/base.cpp")

add_library(slipperyggOBJ OBJECT ${SOURCES})
set_target_properties(slipperyggOBJ PROPERTIES POSITION_INDEPENDENT_CODE ON)
target_precompile_headers(slipperyggOBJ PRIVATE "${CMAKE_SOURCE_DIR}/slipperygg/pch.h")

if (NOT WIN32)
  find_package(Freetype REQUIRED)
  find_package(SDL2 REQUIRED)
  find_package(OpenGL REQUIRED)
  target_link_libraries(slipperyggOBJ PRIVATE SDL2 GL ${CMAKE_DL_LIBS} Freetype::Freetype)
  target_include_directories(slipperyggOBJ PRIVATE ${SDL2_INCLUDE_DIRS} ${FREETYPE_INCLUDE_DIRS})
  target_compile_options(slipperyggOBJ PRIVATE -flto)
  target_link_options(slipperyggOBJ PUBLIC -flto LINKER:--no-undefined)
else()
  target_compile_definitions(slipperyggOBJ PRIVATE _USE_MATH_DEFINES)
  target_include_directories(slipperyggOBJ PRIVATE "${CMAKE_SOURCE_DIR}/slipperygg/freetype")
  target_compile_options(slipperyggOBJ PRIVATE /utf-8)
  add_subdirectory(slipperygg/freetype)
  target_link_libraries(slipperyggOBJ PRIVATE FreeType)
  add_subdirectory(slipperygg/Resources/Shaders)
  add_dependencies(slipperyggOBJ Shaders)
endif()

add_library(slipperygg SHARED "${CMAKE_SOURCE_DIR}/slipperygg/base.cpp")
target_link_libraries(slipperygg PRIVATE slipperyggOBJ)

if (NOT WIN32)
  target_link_options(slipperygg PRIVATE -flto)
else()
  target_link_options(slipperygg PRIVATE /ENTRY:DllEntryPoint)
endif()

option(BUILD_TESTS "Build unit tests" OFF)

if (BUILD_TESTS)
  enable_testing()
  add_subdirectory(Tests)
endif()
